@echo off
@cd /d "%~dp0"
goto checkAdmin

:checkAdmin
	net session >nul 2>&1
	if %errorLevel% == 0 (
		echo.
	) else (
		echo Administrative rights are required, please re-run this script as Administrator.
		goto end
	)

:CheckDLLContinue
	echo Checking for 64-bit Virtual Cam registration...
	reg query "HKLM\SOFTWARE\Classes\CLSID\{@VIRTUALCAM_GUID@}" >nul 2>&1
	if %errorLevel% == 0 (
		echo 64-bit Virtual Cam found, skipping install...
		echo.
	) else (
		echo 64-bit Virtual Cam not found, installing...
		goto install64DLL
	)
	goto endSuccess

:install64DLL
	echo Installing 64-bit Virtual Cam...
	if exist "%~dp0\data\camera\virtualcam-module.dll" (
		regsvr32.exe /i /s "%~dp0\data\camera\virtualcam-module.dll"
	) else (
		regsvr32.exe /i /s virtualcam-module.dll
	)
	reg query "HKLM\SOFTWARE\Classes\CLSID\{@VIRTUALCAM_GUID@}" >nul 2>&1
	if %errorLevel% == 0 (
		echo Virtual Cam successfully installed
		echo.
		goto endSuccess
	) else (
		echo Virtual Cam installation failed
		echo.
		goto end
	)

:endSuccess
	echo Virtual Cam installed!
	echo.

:end
	pause
	exit
